cmake_minimum_required(VERSION 3.7)

include(cmake/HunterGate.cmake)
HunterGate(
    URL "https://github.com/ruslo/hunter/archive/v0.19.34.tar.gz"
    SHA1 "9d0944566336eb6e4e26b4a709e1f17923a9aaa8"
    LOCAL # <----- Use local config.cmake
)

project(GLBrebis)

################################################################################
# OpenSSL / Crypto Libraries
################################################################################
hunter_add_package(OpenSSL)
find_package(OpenSSL REQUIRED)

################################################################################
# Poco C++ Libraries
################################################################################
hunter_add_package(PocoCpp)
find_package(Poco REQUIRED Foundation JSON Net NetSSL Crypto Util Zip CONFIG)

################################################################################
# RapidXML
################################################################################
hunter_add_package(RapidXML)
find_package(RapidXML REQUIRED CONFIG)

################################################################################
# GLBrebis Console App
################################################################################

if (NOT EXISTS ${GLBrebis_SOURCE_DIR}/gl.xml)
    file(DOWNLOAD https://raw.githubusercontent.com/KhronosGroup/OpenGL-Registry/master/xml/gl.xml ${GLBrebis_SOURCE_DIR}/gl.xml SHOW_PROGRESS)
endif()

function(EmbedResource RESOURCE)
    get_filename_component(RESOURCE_WORKSPACE ${RESOURCE} DIRECTORY)
    add_custom_command(
        OUTPUT
            ${RESOURCE}.c
            ${RESOURCE}.h
        COMMAND ${CMAKE_COMMAND}
            -Dresource=${RESOURCE}
            -P ${GLBrebis_SOURCE_DIR}/cmake/EmbedResource.cmake
        WORKING_DIRECTORY
            ${RESOURCE_WORKSPACE}
        MAIN_DEPENDENCY
            ${RESOURCE}
    )
    set_source_files_properties(${RESOURCE}.c PROPERTIES GENERATED TRUE)
    set_source_files_properties(${RESOURCE}.h PROPERTIES GENERATED TRUE)
endfunction()

EmbedResource(${GLBrebis_SOURCE_DIR}/gl.xml)
EmbedResource(${GLBrebis_SOURCE_DIR}/template/GL.c.template)
EmbedResource(${GLBrebis_SOURCE_DIR}/template/GL.h.template)

set(CMAKE_INCLUDE_CURRENT_DIR ON)

add_executable(GLBrebis
    src/GLBrebisApp.cpp
    src/GLBrebisCodeGenerator.cpp
    src/GLBrebisData.cpp
    src/GLBrebisParser.cpp
    src/GLBrebisUtilities.cpp
    gl.xml.c #generated by EmbedResource.cmake
    template/GL.c.template.c #generated by EmbedResource.cmake
    template/GL.h.template.c #generated by EmbedResource.cmake
)

target_link_libraries(GLBrebis
    RapidXML::RapidXML
    Poco::NetSSL
    Poco::Net
    Poco::Foundation
    OpenSSL::SSL
    OpenSSL::Crypto
)

################################################################################
# GLBrebis Example App
################################################################################
add_subdirectory(example)
